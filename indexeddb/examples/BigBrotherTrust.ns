Newspeak3
'Root'
class BigBrotherTrust usingPlatform: p indexedDB: idb <IndexedDB>
(* :exemplar: BigBrotherTrust usingPlatform: platform indexedDB: (IndexedDB usingPlatform: platform) *)
= (
|
	private List = p collections List.
    private Color = p graphics Color.
    private Gradient = p hopscotch Gradient.
	private Presenter = p hopscotch Presenter.
    private Subject = p hopscotch Subject.

	public JSObject = p js global at: 'Object'.
    private Date = p js global at: 'Date'.
    private JSNumber = p js global at: 'Number'.

    private IDBObject = idb IDBObject.
    private IDBSubject = idb IDBSubject.

    public version = 1.
	public transactions = List new.
|
) (
public class BankTransaction amount: a <String>
                                                    onDate: d <Integer>
                                                    memo: m <String>
                                                    reconciled: r <Boolean> = IDBObject (
	|
    public amount = a.
    public txDate = d.
    public memo ::= m.
    public reconciled ::= r.
    |
) (
public transactionDate = (
			^(Date new: txDate) toDateString
		)
) : (
public amount: a = (
	^ amount: a onDate: Date now memo: '' reconciled: false.
)
)
class BankTransactionPresenter onSubject: s =  Presenter onSubject: s (
		) (
isMyKind: p <Presenter> ^ <Boolean> = (
		  ^p isKindOfBankTransactionPresenter
		)
isKindOfBankTransactionPresenter = (
			^true
		)
style = (
	^'font-family: sans-serif; font-size: 110%; color: slateGray'
)
deleteButton = (
	^button: 'Delete' action: [ updateGUI: [ subject delete ] ]
)
definition ^ <Fragment> = (
	^transactionBlock: transactionRow
)
editButton = (
	^button: 'Edit' action: [
    	| m = prompt: 'Edit memo' input: subject memo. |
        updateGUI: [ subject edit: m ].
    ]
)
memoColumn = ( | lbl = label: subject memo. c |
	lbl visual at: 'style' put: style.
	c:: column: { lbl }.
    c visual at: 'style' put: 'width:20%'.
    ^c
)
dateColumn= (| lbl = label: subject transactionDate. c |
	lbl visual at: 'style' put: style.
	c:: column: { lbl }.
    c visual at: 'style' put: 'width:10%'.
    ^c
)
transactionRow = (
	^row: {
			dateColumn.
            blank: 50.
            typeColumn.
            blank: 50.
            amountColumn.
            blank: 50.
            memoColumn.
            blank: 25.
            reconciledColumn.
            blank: 50.
            editButton.
            deleteButton.
	}
)
reconciledColumn = (
	| c |
    c:: column: { checkbox: 'Reconciled' value: subject reconciled action: [ :ck | updateGUI: [ subject toggleReconciled ] ] }.
    c visual at: 'style' put: 'width:10%'.
    ^c
)
typeColumn = ( | lbl = label: subject transactionType. c |
	lbl visual at: 'style' put: style.
	c:: column: { lbl }.
    c visual at: 'style' put: 'width: 5%'.
    ^c
)
transactionBlock: row = (
	^(padded: row with: { 5. 5. 5. 5. }) color: (Color r: 0.99 g: 0.99 b: 0.99 a: 1).
)
amountColumn = (| lbl = label: ((JSNumber new: (subject amount / 100)) toFixed: 2). c |
	lbl visual at: 'style' put: style, 'text-align: right'.
	c:: column: { lbl }.
    c visual at: 'style' put: 'width: 10%'.
    ^c
)
) : (
)
public class BankTransactionSubject onModel: t <BankTransaction> parent: s <IDBSubject> = Subject onModel: t (
	|
    parent = s.
    |
) (
public createPresenter = (
			^BankTransactionPresenter onSubject: self
		)
isKindOfBankTransactionSubject = (
	^true
)
isMyKind: s = (
	^s isKindOfBankTransactionSubject
)
public amount = (
			^model amount
		)
public transactionDate = (
 ^model transactionDate
)
public memo = (
	^model memo
)
public reconciled = (
	^model reconciled
)
public toggleReconciled = (
	(* Do not allow to "unreconcile" *)
	model reconciled ifFalse: [ model reconciled: true. parent edit: model ]
)
public edit: theMemo <String> = (
	model memo: theMemo.
    parent edit: model.
)
public delete = (
	parent delete: model
)
public transactionType = (
	^amount < 0 ifTrue: [ 'Withdraw:' ] ifFalse: [ 'Deposit:' ]
)
) : (
)
public class BankAccountSubject onModel: b <BigBrotherTrust> 
														 idbObjectClasses: i <Array[Class[IDBObject]]> 
= IDBSubject onModel: b 
						idbObjectClasses: i (
) (
withdrawals = (
		^txSubjets select: [ :t | t amount < 0 ]
	)
public createPresenter = (
		^BankAccountPresenter onSubject: self
	)
deposits = (
		^model transactions select: [ :t | t amount > 0 ]
	)
public balance = (
		^model balance
	)
public transactionSubjects = (
	^transactions collect: [ :t | BankTransactionSubject onModel: t parent: self ]
)
onGetAll: event <Alien> = (
	| txs = super onGetAll: event. |
    txs do: [ :tx | transactions add: tx ].
    presenter refresh.
)
add: b <BankTransaction> = (
        add: b
        	onComplete: [ :e |
            	'Tx complete' out.
            	model transact: b.
                presenter refresh.
                nil ]
            onError: [ :e | presenter alert: ((e at: 'error') at: 'message'). nil ]
)
transact: amt withMemo: m = (| b s d |
		d:: Date now.
		b:: BankTransaction amount: amt onDate: d memo: m reconciled: false.
        add: b
)
transact: amt = (
		transact: amt withMemo: ''
)
public deposit: amt withMemo: m = (
	transact: amt withMemo: m.
)
public deposit: amt = (
	deposit: amt withMemo: ''.
)
public withdraw: amt = (
	withdraw: amt withMemo: ''
)
public withdraw: amt withMemo: m = (
	amt > balance ifTrue: [ signal: 'Insufficient funds available.' ]
	ifFalse: [ transact: (amt * -1) withMemo: m ]
)
public edit: transaction = (
	put: transaction
    	onComplete: [ :e | presenter refresh. nil. ]
        onError: [ :e | Error signal: ((e at: 'error') at: 'message'). nil. ]
)
public delete: o = (
	delete: o
    	onComplete: [ :e | transactions remove: o.  presenter refresh. nil ]
        onError: [ :e |  Error signal: 'Failed to delete transaction: ', ((e at: #error) at: 'message'). nil ]
)
) : (
)
class BankAccountPresenter onSubject: s <BankAccountSubject> = Presenter onSubject: s (|
	stFlag = false.
|) (
isKindOfBankAccountPresenter = (
			^true
		)
isMyKind: p <Presenter> ^ <Boolean> = (
		  ^p isKindOfBankAccountPresenter
		)
transactionList = (
			^column:  transactionPresenters.
		)
minorHeadingBlock: body = (
	^(padded: body with: {50. 25. 25. 25.})
	color: minorHeadingColor.
)
majorHeadingBlock: body = (
	^(padded: body with: {50. 25. 25. 25.}) color: majorHeadingColor.
)
header = ( | l = label: 'Your Money'. |
	l visual at: 'style' put: 'font-size: 200%'.
		^row: {  l. }
		)
logoColor = (
	^Color white
)
public refresh = (
	updateGUI: [ ].
)
labelTransactions = ( | lbl = label: 'Transactions'. |
	lbl visual at: #style put: labelStyle.
	^ lbl
)
minorHeadingColor = (
	^Color r: 0.8 g: 0.8 b: 0.8 a: 1.
)
transactionPresenters = (
	^subject transactionSubjects collect: [ :s | s presenter ]
)
majorHeadingColor = (
	^Color r: 0.5 g: 0.5 b: 0.5 a: 1
)
labelStyle = (
	^ 'font-family: sans-serif; font-size: 125%; color: green'
)
transactionsHeader = ( | lbl = label: 'Transactions'. |
	lbl visual at: #style put: labelStyle.
	^row: { lbl }.
)
currentBalance ^<Fragment> = (| lbl b bal n |
	bal:: Float parse: ((subject balance / 100) printString).
	n:: (JSNumber new: bal) toFixed: 2. n out.
	lbl:: label: 'Balance: $'.
    lbl visual at: 'style' put: 'font-family: sans-serif; font-size: 150%; text-align: center'.
    b:: label: n.
	b visual at: 'style' put: 'font-family: sans-serif; font-size: 150%; text-align: center'.
			^row: { lbl. blank: 20. b }
)
labelWithdrawal = (| lbl =  label: 'New Withdrawal:'. |
	lbl visual at: #style put: labelStyle.
    ^lbl
)
labelDeposit = (| lbl = label: 'New Deposit:'. |
	lbl visual at: #style put: labelStyle.
    ^lbl
)
computeDigits: ds = (
	| n |
    '' = ds ifTrue: [ ^0 ].
    n:: [ Float parse: ds ] on: Error do: [ n: 0 ].
   ^n * 100
)
newDepositInput ^ <Fragment> = (
	^button: 'Record a deposit' action: [ | a m v |
    	a:: prompt: 'Enter amount' input: ''.
        m:: prompt: 'Enter a memo' input: ''.
        v:: computeDigits: a.
        0 = v
        	ifFalse: [  updateGUI: [subject deposit: v withMemo: m ]]
            ifTrue: [ alert: 'Invalid amount. Try again' ]
	].
)
inputRow = (| r style |
	r:: row: {
    	padded: newDepositInput with: { 50. 10. 25. 10 }.
        blank: 25.
        padded: newWithdrawalInput with: { 25. 10. 25. 10. }.
        (* button: 'Test' action: [ subject test1000. updateGUI: [ ]] *)
    }.
	style:: r visual at: 'style'.
	style setProperty: 'width' value: '100%'.
    ^r
)
public title = (
	^'BigBrotherTrust'
)
logo = (| b = label: 'Big Brother Trust'. c = label: 'Because he''s watching more than you...'. |
	b visual at: 'style' put: 'font-family: sans-serif; font-size: 200%; text-align: center'.
    c visual at: 'style' put: 'font-family: sans-serif; text-align: center; font-style: italic;color: gray'.
	^column: { row: { b }. row: { c } }
)
logoBlock: body = (
	^(padded: body with: { 100. 50. 100. 50. }) color: logoColor.
)
public definition ^<Fragment> = (
	^column: {
    	logoBlock: logo.
		majorHeadingBlock: header.
		minorHeadingBlock: currentBalance.
        inputRow.
		row: { labelTransactions }.
		transactionList.
        row: {label: 'For entertainment purposes only. No warranty expressed or implied.' }.
   }
)
newWithdrawalInput ^ <Fragment> = (
	^button: 'Record a withdrawal' action: [ | a m v |
    	a:: prompt: 'Enter amount' input: ''.
        v:: computeDigits: a.
        (0 = v or: [ v > subject balance ])
        	ifTrue: [ (0 = v) ifTrue: [ alert: 'Invalid input. Try again.' ]
            						ifFalse: [ alert: 'Insufficient funds available.' ]]
            ifFalse: [
 				m:: prompt: 'Enter a memo' input: ''.
				updateGUI: [subject withdraw: v withMemo: m ]]]
)
) : (
)
public transact: b <BankTransaction> = (
	^transactions add: b
)
public balance = (| rslt = 0.0. |
	(* 1 to: (transactions size) do: [ :i | rslt: rslt + ((transaction at: i) amount) ] .
		^rslt *)
        ^transactions inject: 0.0 into: [ :s :t | s + t amount ].
	)
) : (
)
