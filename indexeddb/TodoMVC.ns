Newspeak3
'Root'
class TodoMVC usingPlatform: p <HopscotchPlatform> indexedDB: idb <IndexedDB> = (
  |
  (* imports *)
  private List = p collections List.
  private Presenter = p hopscotch core Presenter.
  private Subject = p hopscotch core Subject.
  
  private indexedDB = idb.
  private IDBObject = idb IDBObject.
  private IDBSubject = Subject mixinApply: idb IDBSubject.
  
  private crypto = p js global at: 'crypto'.
  public todos = List new.
  |
) (
class TodoItem named: s <String> completed: c uuid: u = IDBObject uuid: u (
|
	public description ::= s.
	public completed <Boolean> ::= c.
|
) (
public active ^ <Boolean> = (
  ^completed not
)
) : (
public new: array = (
	^named: (array at: 1) completed: (array at: 2) uuid: (array at: 0)
)
)
class TodoItemPresenter onSubject: s <TodoItemSubject> =  Presenter onSubject: s (
) (
definition ^ <Fragment> = (
  ^row: {completedCheckBox. blank: 20. description }
)
description ^ <Fragment> = (
  ^subject inEditMode 
     ifFalse: [button: subject description action: [updateGUI: [subject edit]]]
     ifTrue: [| cm = codeMirror: subject description. |
	  cm acceptResponse: [:ed |
		updateGUI: [
			subject description: ed textBeingAccepted.
			ed leaveEditState.
			subject leaveEditMode.
		]
	   ].
	   cm
	]
)
public isKindOfTodoItemPresenter ^ <Boolean> = (
  ^true
)
isMyKind: p <Presenter> ^ <Boolean> = (
  ^p isKindOfTodoItemPresenter
)
completedCheckBox = (
  ^checkbox: 'Complete' 
        value: subject completed 
        action: [ :checked | updateGUI: [subject toggleState]].
)
) : (
)
class TodoItemSubject onModel: ti <TodoItem> = Subject onModel: ti (
  | public inEditMode <Boolean> ::= false. |
) (
public active ^ <Boolean> = (
  ^model active
)
public addTodoNamed: n <String> = (
  todos add: (TodoItem named: n)
)
public completed ^ <Boolean> = (
  ^model completed
)
createPresenter ^ <TodoItemPresenter> = (
  ^TodoItemPresenter onSubject: self
)
public description ^ <String> = (
  ^model description
)
public edit = (
  inEditMode: true
)
public isKindOfTodoItemSubject ^ <Boolean> = (
  ^true
)
isMyKind: p <Presenter> ^ <Boolean> = (
  ^p isKindOfTodoItemSubject
)
public leaveEditMode = (
  inEditMode: false
)
public toggleState = (
  model completed
     ifTrue: [model completed: false. ]
     ifFalse: [model completed: true].
	put: model 
)
public description: d <String> = (
  model description: d.
  put: model.
)
) : (
)
class TodoMVCPresenter onSubject: s <TodoMCSubject> = Presenter onSubject: s (
) (
clearCompleted ^ <Fragment> = (
  ^subject completedTodos size > 0 ifTrue: [
	button: 'Clear Completed' action: [updateGUI: [subject clearCompleted]]
	] ifFalse: [nothing]
)
definition ^ <Fragment> = (
  ^column: {
      header.
      newTodoInput.
      selectedTodos.
      row: {itemsLeft. filters. clearCompleted }
  }
)
header = (
  ^label: 'Todos'
)
public isKindOfTodoMVCPresenter ^ <Boolean> = (
  ^true
)
isMyKind: p <Presenter> ^ <Boolean> = (
  ^p isKindOfTodoMVCPresenter
)
itemsLeft ^ <Fragment> = (
  | 
  activeNumber = subject activeTodos size.
  items <String> = activeNumber  = 1 ifTrue: ['item'] ifFalse: ['items']. 
  |
 ^label: activeNumber printString, ' ', items, ' left'
)
newTodoInput ^ <Fragment> = (
  | cm = codeMirror: ''. |
  cm acceptResponse: [:ed |
	updateGUI: [
		subject addTodoNamed: ed textBeingAccepted.
		ed text: ''.
		ed leaveEditState.
		]
	].
  ^cm
)
selectedTodos = (
  (* Issue: creating new subjects each time destroys subject state *)
  ^column: (subject selectedTodos collect: [:ti <TodoItemSubject> |  ti presenter])
)
filters = (
  ^row: { 
               radioButton: 'All' 
                 value: subject filterState = #all
                 group: 'filter-todos'
                 action: [ :checked | updateGUI: [subject filterAll]
               ].
               mediumBlank.
               radioButton: 'Active' 
                 value: subject filterState = #active
                 group: 'filter-todos'
                 action: [ :checked | updateGUI: [subject filterActive].
               ].
               mediumBlank.               
               radioButton: 'Completed' 
                 value: subject filterState = #completed
                 group: 'filter-todos'
                 action: [ :checked | updateGUI: [subject filterCompleted].
               ]. 
               mediumBlank.    
   }
)
) : (
)
public class TodoMVCSubject onModel: l = IDBSubject onModel: l (
  | 
  public filterState <Symbol> ::= #all. 
  |
) (
public activeTodos ^ <List[TodoItemSubject]> = (
  ^todoSubjects select: [:ti | ti active]
)
public allTodos ^ <List[TodoItemSubject]> = (
  ^todoSubjects copyFrom: 1 to: todoSubjects size
)
public clearCompleted = (
  allTodos do: [:ti <TodoItemSubject> |  ti completed ifTrue: [
		todoSubjects remove: ti.
		todos remove: ti model
		]
	]
)
public completedTodos ^ <List[TodoItemSubject]> = (
  ^todoSubjects select: [:ti | ti completed]
)
createPresenter ^ <TodoMVCPresenter> = (
  ^TodoMVCPresenter onSubject: self
)
public filterActive = (
  filterState:: #active
)
public filterAll = (
  filterState:: #all
)
public filterCompleted = (
  filterState:: #completed
)
public isKindOfTodoMVCSubject ^ <Boolean> = (
  ^true
)
isMyKind: p <Presenter> ^ <Boolean> = (
  ^p isKindOfTodoMVCSubject
)
public selectedTodos ^ <List[TodoItemSubject]> = (
  filterState = #all ifTrue: [^allTodos].
  filterState = #active ifTrue: [^activeTodos].
  ^completedTodos
)
modelClasses = (
	^ { TodoItem. }
)
onGetAll: e = (
	| ts = super onGetAll: e. |
    ts do: [ :t | todos add: t ]
)
public addTodoNamed: n <String> = (
  | newTodo = TodoItem named: n completed: false uuid: crypto randomUUID. |
  add: newTodo 
  	onComplete: [ :e |
  		todos add: newTodo.
  		todoSubjects add: (TodoItemSubject onModel: newTodo).
        nil
  	]
    onError: [ :e | alert: 'Failed to add new Todo'. nil ]
)
todoSubjects = (
	^todos collect: [:ti <TodoItem> | TodoItemSubject onModel: ti]. 
)
) : (
public new = (
  ^onModel: List new
)
)
) : (
)
